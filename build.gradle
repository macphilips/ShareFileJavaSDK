plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

String shareFileJarName = 'sharefile-api'
String shareFileJarVersion = '1.0'

group = 'com.github.macphilips'
version = shareFileJarVersion

allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'org.apache.httpcomponents:httpcore:4.3.2'
    compile 'org.apache.directory.studio:org.apache.commons.codec:1.6'

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

jar {
    baseName = shareFileJarName
    version = shareFileJarVersion
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    baseName = shareFileJarName
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    baseName = shareFileJarName
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = shareFileJarName
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'ShareFile-Api'
                description = 'ShareFile Java SDK using the V3 API'
                url = 'https://github.com/macphilips/ShareFileJavaSDK'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://github.com/macphilips/ShareFileJavaSDK/blob/master/LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        id = 'nileshp'
                        name = 'Nilesh Pawar'
                        email = 'nilesh.pawar@citrix.com'
                    }
                    developer {
                        id = 'macphilips'
                        name = 'Morolari Titilope'
                        email = 'tmorolari@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/macphilips/ShareFileJavaSDK.git'
                    developerConnection = 'scm:git:ssh://github.com/macphilips/ShareFileJavaSDK.git'
                    url = 'https://github.com/macphilips/ShareFileJavaSDK'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
